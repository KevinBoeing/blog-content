<p>In the <a href="/learning-webassembly-6-running-wasm-in-nodejs" target="_blank">previous part</a> of this <a href="/learning-webassembly-series" target="_blank">series</a>, we executed Wasm modules in Node.js, a popular JavaScript backend platform. In this part, we will see how to make system calls from Wasm.</p>

<p><a href="https://wasi.dev" target="_blank">WebAssembly System Interface</a> (WASI) is a family of APIs designed as a new standard engine-independent non-web system-oriented interface for WebAssembly. It enables working with files, networking, and other operating system features directly from Wasm.</p>

<p>WASI focuses highly on <strong>security</strong> and <strong>portability</strong>. In fact, these principles are baked explicitly into the WASI APIs.</p>

<h2>Sandboxing</h2>

<p>WebAssembly is <em>sandboxed</em>. This means that Wasm code can’t talk directly to the operating system. In order for Wasm to do anything with system resources, the host (browser or another runtime like Node.js or Wasmtime) must <a href="/learning-webassembly-5-running-wasm-in-the-browser" target="_blank">import functions</a> in the sandbox that the code can use.</p>

<p>In this way, the host can limit what a program can do on a program-by-program basis.</p>

<p>Albeit sandboxing makes systems more secure, the host can still import capabilities that had better not be there. WASI gives us a way to extend this security even further.</p>

<h2>Security</h2>

<p>With WASI, it is possible to attach permissions to different resources on a module-by-module basis. By default, a module has no access to any resource.</p>

<p>When a module needs to access a file in a specific directory, the permission must be explicitly passed to the module:</p>

<pre>
var wasi = new WASI({
  preopens: {
    '/sandbox': '/real/path/that/wasm/can/access'
  }
});
</pre>

<p>This brings Wasm closer to the <a href="https://en.wikipedia.org/wiki/Principle_of_least_privilege" target="_blank">principle of least privilege</a>, where a module can only access the exact resources it needs to do its job.</p>

<h2>Portability</h2>

<p>Wasm is a <em>portable</em> binary format. This means Wasm code could be compiled once and run across a whole bunch of different machines.</p>

<p>The same Wasm module could be executed in a <a href="https://wasi.dev/polyfill" target="_blank">browser</a> and, for example, <a href="https://nodejs.org/api/wasi.html" target="_blank">Node.js</a> runtime.</p>

<h2>WASI in Node.js</h2>

<p>Starting with version 12, Node.js provides an implementation of WASI:</p>

<pre>
const fs = require('fs');
const { WASI } = require('wasi');

const wasi = new <b>WASI</b>({
  args: process.argv,
  env: process.env,
  preopens: {
    '/sandbox': '/some/real/path'
  }
});

const importObject = {
  <b>wasi_unstable: wasi.wasiImport</b>
};

(async () => {
  const wasm = await WebAssembly
    .compile(fs.readFileSync('hello.wasm'));

  const instance = await WebAssembly
    .instantiate(wasm, importObject);

  <b>wasi.start</b>(instance);
})();
</pre>

<p>The following Wat code shows using WASI to print a string to the standard output:</p>

<pre>
(module
  (import "wasi_unstable" "<b>fd_write</b>"
    (func $fd_write (param i32 i32 i32 i32)
                    (result i32)))

  (memory 1)
  (export "memory" (memory 0))
  (data (i32.const 8) "hello\n")

  (func $main (export "_start")

    ;; io vector within memory
    (i32.store (i32.const 0) (i32.const 8))
    (i32.store (i32.const 4) (i32.const 6))

    (call <b>$fd_write</b>
        (i32.const 1)  ;; file_descriptor
        (i32.const 0)  ;; *iovs
        (i32.const 1)  ;; iovs_len
        (i32.const 14) ;; nwritten
    )
    drop ;; drop the result from the stack
  )
)
</pre>

<p>As WASI is an experimental feature, the <code>--experimental-wasi-unstable-preview1</code> CLI argument is needed for this example to run:</p>

<pre>
$ node --version
v14.15.3

$ node --experimental-wasi-unstable-preview1 wasi-node.js
hello
</pre>

<p>Let's take a closer look at the code above.</p>

<p>The <code>fd_write</code> function is part of the WASI API and it is imported by the runtime. It takes a file descriptor, a pointer to a list of IO vectors and its length, and an index of a place in <a href="/learning-webassembly-4-wasm-memory-and-working-with-strings" target="_blank">memory</a> to store the number of bytes written as parameters and returns the number of bytes written. The IO vector is the most interesting part.</p>

<p>An <em>IO vector</em> is a structure to describe a piece of data in memory. It consists of two 32-bit integers: the index of the memory chunk where the data starts, and the length of the data in bytes:</p>

<pre>
;; data starts on 8th byte in memory
(i32.store (i32.const 0) (i32.const 8))

;; data "hello\n" has length of 6 bytes
(i32.store (i32.const 4) (i32.const 6))
</pre>

<p>Our vector is stored in 8 bytes of memory starting on index 0.</p>

<p class="note">Why are two 32-bit integers stored in 8 bytes? Easy numbers: one byte has 8 bits, a 32-bit type needs 32 / 8 = 4 bytes; two 32-bit types need 4 * 2 = 8 bytes.</p>

<p>As parameters of the <code>fd_write</code> function, we put <code>1</code> for the stdout file descriptor, <code>0</code> as the memory index of the list IO vectors, <code>1</code> as the length of the list (we have only one string to print / one IO vector in the list), and finally, an empty place in memory to store the number of bytes written (data in the linear memory ends on the 14th byte — 8 + 6 = 14).</p>

<p>And of course, we can run the same code with another WASI runtime like <a href="https://github.com/bytecodealliance/wasmtime" target="_blank">Wasmtime</a> as well:</p>

<pre>
$ wasmtime run hello.wasm
hello
</pre>

<h2>WASI Read and Write</h2>

<p>To demonstrate both read and write WASI capabilities, we write a slightly more advanced <em>Echo</em> program that reads input from stdout and prints it to stdout afterward:</p>

<pre>
(module
  (import "wasi_unstable" "<b>fd_read</b>"
    (func $fd_read
      (param i32 i32 i32 i32)
      (result i32)))
  (import "wasi_unstable" "<b>fd_write</b>"
    (func $fd_write
      (param i32 i32 i32 i32)
      (result i32)))

  (memory 1)
  (export "memory" (memory 0))

  (func $main (export "_start")

    ;; buffer of 100 chars to read into
    (i32.store (i32.const 4) (i32.const 12))
    (i32.store (i32.const 8) (i32.const 100))

    (call <b>$fd_read</b>
      (i32.const 0) ;; 0 for stdin
      (i32.const 4) ;; *iovs
      (i32.const 1) ;; iovs_len
      (i32.const 8) ;; nread
    )
    drop

    (call <b>$fd_write</b>
      (i32.const 1) ;; 1 for stdout
      (i32.const 4) ;; *iovs
      (i32.const 1) ;; iovs_len
      (i32.const 0) ;; nwritten
    )
    drop
  )
)
</pre>

<p>The <code>fd_read</code> function has a familiar signature; it takes a file descriptor, a list of IO vectors to read into, the list length, and a place in memory for the number of bytes read.</p>

<p>This time, we will allocate memory of 100 bytes:</p>

<pre>
(i32.store (i32.const 4) (i32.const 12))
(i32.store (i32.const <b>8</b>) (i32.const <b>100</b>))
</pre>

<p>We update the length of data (on the memory index 8) based on the number of bytes read, so the same data length will be printed afterward:</p>

<pre>
(call $fd_read
  (i32.const 0) ;; 0 for stdin
  (i32.const 4) ;; *iovs
  (i32.const 1) ;; iovs_len
  <b>(i32.const 8) ;; nread</b>
)
</pre>

<p>Without this trick, the whole buffer of 100 bytes would be written to stdout (with trailing null bytes).</p>

<p>It works as expected:</p>

<pre>
$ wasmtime run echo.wasm
<i>hello, wasi!</i>
hello, wasi!
</pre>

<h2>Further Steps</h2>

<p>This time, we have shown how to work with system resources from Wasm using the WASI API.</p>

<p>In the <a href="/learning-webassembly-8-compiling-into-wasm">next part</a> of this <a href="/learning-webassembly-series">series</a> we will leave Wat programming for a while and take a look at how to compile Wasm modules from different programming languages, such as C, Kotlin, and AssemblyScript.</p>

<p>Stay tuned!</p>
